package au.org.ala.sds.validation;

import au.org.ala.sds.model.SensitivityZone;
import au.org.ala.sds.validation.MessageFactory;
import au.org.ala.sds.validation.RuleState;
import au.org.ala.sds.validation.ValidationReport;
import java.util.Set;
import org.apache.log4j.Logger;
import org.drools.runtime.rule.RuleContext;

global ValidationReport validationReport;
global RuleState state;
global Logger logger;

import function au.org.ala.sds.validation.ValidationHelper.*;

rule "In Australia"
    when
        $zones : Set()
        eval(SensitivityZone.isInAustralia($zones))
    then
        state.setLoadable(false);
        state.setAnnotation("Plant pest not known to occur in Australia (or this area of Australia)");
        logger.warn("PBC1 Rule '" + kcontext.getRule().getName() + "'");
        warning(kcontext, MessageFactory.PLANT_PEST_MSG_CAT1_01);
end

rule "In External Territory"
    when
        $zones : Set()
        eval(SensitivityZone.isExternalTerritory($zones))
    then
        state.setLoadable(true);
        state.setAnnotation("Plant pest species in an external territory controlled by Australia");
        logger.warn("PBC1 Rule '" + kcontext.getRule().getName() + "'");
end

rule "In Torres Strait Zone"
    when
        $zones : Set()
        eval(SensitivityZone.isInTorresStrait($zones))
    then        
        state.setDelegateRules("PBC5a");
        state.setComplete(false);
        logger.warn("PBC1 Rule '" + kcontext.getRule().getName() + "'");
end

rule "Not in Australian"
    when
        $zones : Set()
        eval($zones.contains(SensitivityZone.NOTAUS))
    then
        state.setLoadable(false);
        state.setAnnotation("Plant pest species occurrence not in Australia");
        logger.warn("PBC1 Rule '" + kcontext.getRule().getName() + "'");
end